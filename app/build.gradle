plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
}

def uploadKeyStorePassword = ""
def uploadKeyAlias = ""
def uploadKeyPassword = ""
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
if (properties.containsKey('uploadKeyStorePassword')) {
    uploadKeyStorePassword = properties.getProperty('uploadKeyStorePassword')
}
if (properties.containsKey('uploadKeyAlias')) {
    uploadKeyAlias = properties.getProperty('uploadKeyAlias')
}
if (properties.containsKey('uploadKeyPassword')) {
    uploadKeyPassword = properties.getProperty('uploadKeyPassword')
}

android {
    namespace 'ldev.myNotifier'
    compileSdk 33

    signingConfigs {
        config {
            storeFile file('Keys/upload-key.jks')
            storePassword uploadKeyStorePassword
            keyAlias uploadKeyAlias
            keyPassword uploadKeyPassword
        }
    }

    defaultConfig {
        applicationId "ldev.myNotifier"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    // Default
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.fragment:fragment-ktx:1.6.0"
    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.material:material:1.9.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // Navigation Component
    implementation 'androidx.navigation:navigation-fragment-ktx:2.6.0'

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
}